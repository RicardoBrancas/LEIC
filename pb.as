; Passaro Bamboleante

; CONSTANTES
SP_INICIAL	EQU	FDFFh
DISPLAY_O_ADDR	EQU	FFFEh
DISPLAY_C_ADDR	EQU	FFFCh
INT_MASK_ADDR	EQU	FFFAh
CONT_STEP_ADDR	EQU	FFF6h
CONT_CTRL_ADDR	EQU	FFF7h

INT_MASK	EQU	1000000000000011b

COLUNAS		EQU	79
LINHAS		EQU	24

POS_OBST_MAX	EQU	15

FIM_TEXTO	EQU	'@'
CHAR_TRACO	EQU	2Dh
CHAR_O		EQU	4Fh
CHAR_X		EQU	58h
CHAR_>		EQU	3Eh
PHYSICS_STEP	EQU	1
GRAV_POR_STEP	EQU	0000000000010000b ; Gravidade em virgula fixa - 8 casas decimais
V_SUBIDA	EQU	1
LINHAS_SOBE	EQU	4

INTERV_OBSTAC	EQU	5

V_OBSTACULOS_1	EQU	10
V_OBSTACULOS_2	EQU	8
V_OBSTACULOS_3	EQU	6
V_OBSTACULOS_4	EQU	4
V_OBSTACULOS_5	EQU	2
V_OBSTACULOS_6	EQU	1

MASK_SEQUENCIA	EQU	1000000000010110b

; VARIAVEIS
		ORIG	8000h
Vartext1	STR	'Prepare-se', FIM_TEXTO
Vartext2	STR	'Prima I1 para iniciar o jogo', FIM_TEXTO
COMECAR_JOGO	STR	1

N_SEQUENCIA	STR	0

P_LINHA_ATUAL	STR	11
P_COLUNA_ATUAL	STR	19
LINHA_ATUAL	STR	0000101100000000b ; 11d em virgula fixa - 8 casas decimais
FALTA_SUBIR	STR	0

VELOCIDADE	STR	0

V_OBSTACULOS	STR	0
CONT_MOV_OBST	STR	0
CONTADOR_OBST	STR	0

OBSTACULOS	TAB	COLUNAS

DEVE_SUBIR	STR	0
DEVE_CRIAR_OSBT	STR	0
DEVE_MOVER_OBST	STR	0
DEVE_PYSC_STEP	STR	0

; Tabela de interrupcoes
		ORIG	FE00h
INT0		WORD	i0Premido
		ORIG	FE01h
INT1		WORD	i1Premido
		ORIG	FE0Fh
INT15		WORD	tempAtivado

		ORIG	0000h
		JMP	Inicio

i0Premido:	MOV	R1, LINHAS_SOBE
		MOV	M[DEVE_SUBIR], R1
		RTI

i1Premido:	MOV	M[COMECAR_JOGO], R0
		RTI

tempAtivado:	MOV	R1, 1
		MOV	M[DEVE_PYSC_STEP], R1
		CMP	M[CONT_MOV_OBST], R0
		BR.P	tempAt2
		MOV	R1, 1
		MOV	M[DEVE_MOVER_OBST], R1
tempAt2:	DEC	M[CONT_MOV_OBST]
		MOV	R7, PHYSICS_STEP
		MOV	M[CONT_STEP_ADDR], R7
		MOV	R7, 1
		MOV	M[CONT_CTRL_ADDR], R7
FimTempAtivado:	RTI

Random:		MOV	R1, M[N_SEQUENCIA]
		SHR	R1, 1
		BR.C	Random2
		MOV	M[N_SEQUENCIA], R1
		BR	RandomFim
Random2:	MOV	R1, M[N_SEQUENCIA]
		XOR	R1, MASK_SEQUENCIA
		SHR	R1, 1
		MOV	M[N_SEQUENCIA], R1
RandomFim:	MOV	R2, POS_OBST_MAX
		DIV	R1, R2
		INC	R2
		MOV	M[SP+2], R2
		RET

EscreveChar:	PUSH 	R1
		PUSH 	R2
		PUSH 	R3
		MOV 	R1, M[SP+7] ;Linha
		MOV 	R2, M[SP+6] ;Coluna
		MOV 	R3, M[SP+5] ;Caracter
		SHL	R1, 8
		ADD	R1, R2
		MOV	M[DISPLAY_C_ADDR], R1
		MOV	M[DISPLAY_O_ADDR], R3
		POP	R3
		POP	R2
		POP	R1
		RETN	3

EscreveStr:	PUSH	R1
		PUSH	R2
		PUSH	R3
		PUSH	R4
		MOV	R1, M[SP+8] ;EndereÃ§o Inicial
		MOV	R2, M[SP+7] ;Linha Inicial
		MOV	R3, M[SP+6] ;Coluna Inicial
CicloEscreveStr:MOV	R4, M[R1]
		CMP	R4, FIM_TEXTO
		BR.Z	FimEscreveStr
		PUSH	R2
		PUSH	R3
		PUSH	R4
		CALL	EscreveChar
		INC	R1
		INC	R3
		BR	CicloEscreveStr
FimEscreveStr:	POP	R4
		POP	R3
		POP	R2
		POP	R1
		RETN	3

LimpaJanela:	MOV	R1, R0
		MOV	R2, R0
CicloLinhas:	CMP	R1, LINHAS
		BR.NN	FimAJ
CicloColunas:	CMP	R2, COLUNAS
		BR.NN	FimColunas
		PUSH	R1
		PUSH	R2
		PUSH	20h
		CALL	EscreveChar
		INC	R2
		BR	CicloColunas
FimColunas:	MOV	R2, R0
		INC	R1
		BR	CicloLinhas
FimAJ:		RET

AtualizaJanela:	CALL	LimpaJanela
		CALL	DesenhaLimites
		CALL	DesenhaPassaro
		RET

; DesenhaLimites: Esta funcao desenha os limites superior e infeior do ecra de jogo
; Variavies:	R1: linha do limite a ser desenhado
;		R2: coluna atual
;		R3: contem a linha atual, apos um shift left, 8 posicoes
;		R4
;		R5: Contem o CHAR_TRACO
DesenhaLimites:	MOV	R5, CHAR_TRACO
		MOV	R1, 0
		MOV	R2, 0
		MOV	R3, R1
		SHL	R3, 8
DL1:		CMP	R2, COLUNAS
		BR.P	FimDL1
		MOV	R4, R2
		ADD	R4, R3
		MOV	M[DISPLAY_C_ADDR], R4
		MOV	M[DISPLAY_O_ADDR], R5
		INC	R2
		BR	DL1
FimDL1:		NOP
		MOV	R1, 23
		MOV	R2, 0
		MOV	R3, R1
		SHL	R3, 8
DL2:		CMP	R2, COLUNAS
		BR.P	FimDL2
		MOV	R4, R2
		ADD	R4, R3
		MOV	M[DISPLAY_C_ADDR], R4
		MOV	M[DISPLAY_O_ADDR], R5
		INC	R2
		BR	DL2
FimDL2:		NOP
		RET

; DesenhaPassaro: 
DesenhaPassaro:	MOV	R1, M[P_LINHA_ATUAL]
		MOV	R2, M[P_COLUNA_ATUAL]
		PUSH	R1
		PUSH	R2
		PUSH	CHAR_O
		CALL	EscreveChar
		INC	R2
		PUSH	R1
		PUSH	R2
		PUSH	CHAR_>
		CALL	EscreveChar
		RET

; LimpaPassaro:
LimpaPassaro:	MOV	R1, M[P_LINHA_ATUAL]
		MOV	R2, M[P_COLUNA_ATUAL]
		PUSH	R1
		PUSH	R2
		PUSH	20h
		CALL	EscreveChar
		INC	R2
		PUSH	R1
		PUSH	R2
		PUSH	20h
		CALL	EscreveChar
		RET

AtualizaObst:	CMP	M[SP+2], R0
		BR.NZ	DesenhaObst
		MOV	R7, 20h
		BR	InicioAO
DesenhaObst:	MOV	R7, CHAR_X
InicioAO:	MOV	R1, R0
		MOV	R2, OBSTACULOS
CicloColAO:	CMP	R1, COLUNAS
		JMP.NN	FimAtualzObst
		CMP	M[R2], R0
		JMP.Z	FimCicloAO
		MOV	R4, LINHAS
		SUB	R4, 2
		SUB	R4, M[R2]
		MOV	R3, LINHAS
		SUB	R3, 2
CicloLinhaAO:	CMP	R3, R4
		BR.NZ	SaltaCicloAO
		SUB	R3, 5
SaltaCicloAO:	PUSH	R3
		PUSH	R1
		PUSH	R7
		CALL	EscreveChar
		DEC	R3
		CMP	R3, R0
		BR.NZ	CicloLinhaAO
FimCicloAO:	INC	R1
		INC	R2
		JMP	CicloColAO
FimAtualzObst:	RETN	1

; MudaPassaro:
;		R1 = novaLinha
;		R2 = novaColuna

MudaPassaro:	CALL	LimpaPassaro
		MOV	R1, M[SP+3]
		MOV	R2, M[SP+2]
		MOV	M[P_LINHA_ATUAL], R1
		MOV	M[P_COLUNA_ATUAL], R2
		CALL	DesenhaPassaro
		RETN	2

CriaObstaculo:	MOV	R1, OBSTACULOS
		ADD	R1, COLUNAS
		DEC	R1
		MOV	R7, PHYSICS_STEP
		MOV	M[CONT_STEP_ADDR], R7
		MOV	R7, 1
		MOV	M[CONT_CTRL_ADDR], R7
		PUSH	R1
		PUSH	R0
		CALL	Random
		POP	R2
		POP	R1
		MOV	M[R1], R2
		MOV	M[DEVE_CRIAR_OSBT], R0
		MOV	R1, INTERV_OBSTAC
		MOV	M[CONTADOR_OBST], R1
		PUSH	R0
		CALL	AtualizaObst
		PUSH	1
		CALL	AtualizaObst
		RET

MoveObstaculos:	PUSH	R0
		CALL	AtualizaObst
		MOV	R1, COLUNAS
		DEC	R1
		MOV	R2, OBSTACULOS
		ADD	R2, R1
		MOV	R3, R0
CicloMoveObst:	CMP	R1, R0
		BR.N	FimCicloMO
		MOV	R4, M[R2]
		MOV	M[R2], R3
		MOV	R3, R4
		DEC	R1
		DEC	R2
		BR	CicloMoveObst
FimCicloMO:	CMP	M[CONTADOR_OBST], R0
		BR.P	tempAt3
		MOV	R1, 1
		MOV	M[DEVE_CRIAR_OSBT], R1
tempAt3:	DEC	M[CONTADOR_OBST]
		MOV	M[DEVE_MOVER_OBST], R0
		MOV	R1, M[V_OBSTACULOS]
		MOV	M[CONT_MOV_OBST], R1
		PUSH	1
		CALL	AtualizaObst
FimMoveObst:	RET

SobePassaro:	MOV	R1, 0000000010010000b
		MOV	M[VELOCIDADE], R1
		MOV	M[DEVE_SUBIR], R0
		RET

PhysicsStep:	MOV	R1, M[VELOCIDADE]
		SUB	R1, GRAV_POR_STEP
		MOV	M[VELOCIDADE], R1
		MOV	R2, M[LINHA_ATUAL]
		SUB	R2, R1
		MOV	M[LINHA_ATUAL], R2
		SHR	R2, 8
		PUSH	R2
		PUSH	M[P_COLUNA_ATUAL]
		CALL	MudaPassaro
		MOV	M[DEVE_PYSC_STEP], R0
		RET

Inicio:		MOV	R7, SP_INICIAL
		MOV	SP, R7
		MOV	R7, INT_MASK
		MOV	M[INT_MASK_ADDR], R7
		ENI
		MOV	R7, FFFFh
		MOV	M[DISPLAY_C_ADDR], R7
		MOV	R1, V_OBSTACULOS_1
		MOV	M[V_OBSTACULOS], R1
		PUSH	Vartext1
		PUSH	11
		PUSH	34
		CALL	EscreveStr
		PUSH	Vartext2
		PUSH	13
		PUSH	25
		CALL	EscreveStr
EsperaInicio:	CMP 	M[COMECAR_JOGO], R0
		BR.Z	ComecaJogo
		INC	M[N_SEQUENCIA]
		BR	EsperaInicio

ComecaJogo:	CALL	AtualizaJanela
		MOV	R7, PHYSICS_STEP
		MOV	M[CONT_STEP_ADDR], R7
		MOV	R7, 1
		MOV	M[CONT_CTRL_ADDR], R7
Ciclo1:		CMP	M[DEVE_PYSC_STEP], R0
		BR.Z	Ciclo2
		DSI
		CALL	PhysicsStep
		ENI
Ciclo2:		CMP	M[DEVE_SUBIR], R0
		BR.Z	Ciclo3
		DSI
		CALL	SobePassaro
		ENI
Ciclo3:		CMP	M[DEVE_CRIAR_OSBT], R0
		BR.Z	Ciclo4
		DSI
		CALL	CriaObstaculo
		ENI
Ciclo4:		CMP	M[DEVE_MOVER_OBST], R0
		BR.Z	Ciclo1
		DSI
		CALL	MoveObstaculos
		ENI
		BR	Ciclo1
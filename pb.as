; Passaro Bamboleante

; CONSTANTES
SP_INICIAL	EQU	FDFFh
CONT_STEP_ADDR	EQU	FFF6h
CONT_CTRL_ADDR	EQU	FFF7h
LEDS_ADDR	EQU	FFF8h
INT_MASK_ADDR	EQU	FFFAh
DISPLAY_C_ADDR	EQU	FFFCh
DISPLAY_O_ADDR	EQU	FFFEh

INT_MASK	EQU	1000000000000111b

COLUNAS		EQU	79
LINHAS		EQU	24

POS_OBST_MAX	EQU	15

FIM_TEXTO	EQU	'@'
CHAR_TRACO	EQU	2Dh
CHAR_O		EQU	4Fh
CHAR_X		EQU	58h
CHAR_>		EQU	3Eh
PHYSICS_STEP	EQU	1
GRAV_POR_STEP	EQU	0000000000010000b ; Gravidade em virgula fixa - 8 casas decimais
IMPULSO_SUBIDA	EQU	0000000010010000b
V_SUBIDA	EQU	1
LINHAS_SOBE	EQU	4

INTERV_OBSTAC	EQU	5

NIVEL_MAXIMO	EQU	5

MASK_SEQUENCIA	EQU	1000000000010110b

; VARIAVEIS
		ORIG	8000h
Vartext1	STR	'Prepare-se', FIM_TEXTO
Vartext2	STR	'Prima I1 para iniciar o jogo', FIM_TEXTO
COMECAR_JOGO	STR	1

N_SEQUENCIA	STR	0

P_LINHA_ATUAL	STR	11
P_COLUNA_ATUAL	STR	19
LINHA_ATUAL	STR	0000101100000000b ; 11d em virgula fixa - 8 casas decimais
FALTA_SUBIR	STR	0

VELOCIDADES	STR	10, 8, 6, 4, 2, 1

VELOCIDADE	STR	0

NIVEL		STR	0

V_OBSTACULOS	STR	0
CONT_MOV_OBST	STR	0
CONTADOR_OBST	STR	0

OBSTACULOS	TAB	COLUNAS

NIVEL_MUDOU	STR	0
DEVE_SUBIR	STR	0
DEVE_CRIAR_OSBT	STR	0
DEVE_MOVER_OBST	STR	0
DEVE_PYSC_STEP	STR	0

; Tabela de interrupcoes
		ORIG	FE00h
INT0		WORD	i0Premido
		ORIG	FE01h
INT1		WORD	i1Premido
		ORIG	FE02h
INT2		WORD	i2Premido
		ORIG	FE0Fh
INT15		WORD	tempAtivado

		ORIG	0000h
		JMP	Inicio

i0Premido:	PUSH	R1
		MOV	R1, LINHAS_SOBE
		MOV	M[DEVE_SUBIR], R1
		POP	R1
		RTI

i1Premido:	MOV	M[COMECAR_JOGO], R0
		CMP	M[NIVEL], R0
		BR.NP	FimI1Primido
		DEC	M[NIVEL]
		INC	M[NIVEL_MUDOU]
FimI1Primido:	RTI

i2Premido:	PUSH	R1
		MOV	R1, M[NIVEL]
		CMP	R1, NIVEL_MAXIMO
		BR.NN	FimI2Primido
		INC	M[NIVEL]
		INC	M[NIVEL_MUDOU]
FimI2Primido:	POP	R1
		RTI

tempAtivado:	DSI
		PUSH	R1
		PUSH	R7
		MOV	R1, 1
		MOV	M[DEVE_PYSC_STEP], R1
		CMP	M[CONT_MOV_OBST], R0
		BR.P	tempAt2
		MOV	R1, 1
		MOV	M[DEVE_MOVER_OBST], R1
tempAt2:	DEC	M[CONT_MOV_OBST]
		MOV	R7, PHYSICS_STEP
		MOV	M[CONT_STEP_ADDR], R7
		MOV	R7, 1
		MOV	M[CONT_CTRL_ADDR], R7
		POP	R7
		POP	R1
		ENI
FimTempAtivado:	RTI

Random:		MOV	R1, M[N_SEQUENCIA]
		SHR	R1, 1
		BR.C	Random2
		MOV	M[N_SEQUENCIA], R1
		BR	RandomFim
Random2:	MOV	R1, M[N_SEQUENCIA]
		XOR	R1, MASK_SEQUENCIA
		SHR	R1, 1
		MOV	M[N_SEQUENCIA], R1
RandomFim:	MOV	R2, POS_OBST_MAX
		DIV	R1, R2
		INC	R2
		MOV	M[SP+2], R2
		RET

EscreveChar:	PUSH 	R1
		PUSH 	R2
		PUSH 	R3
		MOV 	R1, M[SP+7] ;Linha
		MOV 	R2, M[SP+6] ;Coluna
		MOV 	R3, M[SP+5] ;Caracter
		SHL	R1, 8
		ADD	R1, R2
		MOV	M[DISPLAY_C_ADDR], R1
		MOV	M[DISPLAY_O_ADDR], R3
		POP	R3
		POP	R2
		POP	R1
		RETN	3

EscreveStr:	PUSH	R1
		PUSH	R2
		PUSH	R3
		PUSH	R4
		MOV	R1, M[SP+8] ;EndereÃ§o Inicial
		MOV	R2, M[SP+7] ;Linha Inicial
		MOV	R3, M[SP+6] ;Coluna Inicial
CicloEscreveStr:MOV	R4, M[R1]
		CMP	R4, FIM_TEXTO
		BR.Z	FimEscreveStr
		PUSH	R2
		PUSH	R3
		PUSH	R4
		CALL	EscreveChar
		INC	R1
		INC	R3
		BR	CicloEscreveStr
FimEscreveStr:	POP	R4
		POP	R3
		POP	R2
		POP	R1
		RETN	3

LimpaJanela:	MOV	R1, R0
		MOV	R2, R0
CicloLinhas:	CMP	R1, LINHAS
		BR.NN	FimAJ
CicloColunas:	CMP	R2, COLUNAS
		BR.NN	FimColunas
		PUSH	R1
		PUSH	R2
		PUSH	20h
		CALL	EscreveChar
		INC	R2
		BR	CicloColunas
FimColunas:	MOV	R2, R0
		INC	R1
		BR	CicloLinhas
FimAJ:		RET

AtualizaLeds:	MOV	R1, M[NIVEL]
		MOV	R2, R0
		MOV	R3, R0
CicloAtualLeds:	CMP	R1, R2
		BR.N	FimAtualizaLeds
		STC
		RORC	R3, 1
		INC	R2
		BR	CicloAtualLeds
FimAtualizaLeds:MOV	M[LEDS_ADDR], R3
		RET

; DesenhaLimites: Esta funcao desenha os limites superior e infeior do ecra de jogo
; Variavies:	R1: linha do limite a ser desenhado
;		R2: coluna atual
;		R3: contem a linha atual, apos um shift left, 8 posicoes
;		R4
;		R5: Contem o CHAR_TRACO
DesenhaLimites:	MOV	R2, R0
CicloDLim2:	MOV	R1, COLUNAS
		DEC	R1
CicloDLim1:	PUSH	R2
		PUSH	R1
		PUSH	CHAR_TRACO
		CALL	EscreveChar
		DEC	R1
		CMP	R0, R1
		BR.NP	CicloDLim1
		CMP	R2, R0
		BR.NZ	FIMDesenhaLim
		MOV	R2, LINHAS
		DEC	R2
		BR	CicloDLim2
FIMDesenhaLim:	RET

AtualizaPassaro:MOV	R1, M[P_LINHA_ATUAL]
		MOV	R2, M[P_COLUNA_ATUAL]
		CMP	M[SP+2], R0
		BR.NZ	DesenhaPassaro
		MOV	R3, 20h
		MOV	R4, 20h
		BR 	InicioDPassaro
DesenhaPassaro:	MOV	R3, CHAR_O
		MOV	R4, CHAR_>
InicioDPassaro:	PUSH	R1
		PUSH	R2
		PUSH	R3
		CALL	EscreveChar
		INC	R2
		PUSH	R1
		PUSH	R2
		PUSH	R4
		CALL	EscreveChar
		RETN	1

AtualizaObst:	CMP	M[SP+2], R0
		BR.NZ	DesenhaObst
		MOV	R7, 20h
		BR	InicioAO
DesenhaObst:	MOV	R7, CHAR_X
InicioAO:	MOV	R1, R0
		MOV	R2, OBSTACULOS
CicloColAO:	CMP	R1, COLUNAS
		JMP.NN	FimAtualzObst
		CMP	M[R2], R0
		JMP.Z	FimCicloAO
		MOV	R4, LINHAS
		SUB	R4, 2
		SUB	R4, M[R2]
		MOV	R3, LINHAS
		SUB	R3, 2
CicloLinhaAO:	CMP	R3, R4
		BR.NZ	SaltaCicloAO
		SUB	R3, 6
SaltaCicloAO:	PUSH	R3
		PUSH	R1
		PUSH	R7
		CALL	EscreveChar
		DEC	R3
		CMP	R3, R0
		BR.NZ	CicloLinhaAO
FimCicloAO:	INC	R1
		INC	R2
		JMP	CicloColAO
FimAtualzObst:	RETN	1

; MudaPassaro:
;		R1 = novaLinha
;		R2 = novaColuna

MudaPassaro:	MOV	R3, M[P_LINHA_ATUAL]
		CMP	R3, R0
		BR.NP	HaColisao
		CMP	R3, LINHAS
		BR.NN	HaColisao
		MOV	R1, OBSTACULOS
		ADD	R1, M[P_COLUNA_ATUAL]
		CMP	M[R1], R0
		BR.Z	NaoHaColisao
		MOV	R2, LINHAS
		SUB	R2, 2
		SUB	R2, M[R1]
		MOV	R3, R2
		SUB	R3, 6
		CMP	M[P_LINHA_ATUAL], R2
		BR.NN	HaColisao
		CMP	M[P_LINHA_ATUAL], R3
		BR.NP	HaColisao
		BR	NaoHaColisao
HaColisao:	CALL	Colisao
		RETN	2
NaoHaColisao:	PUSH	R0
		CALL	AtualizaPassaro
		MOV	R5, M[SP+3]
		MOV	R6, M[SP+2]
		MOV	M[P_LINHA_ATUAL], R5
		MOV	M[P_COLUNA_ATUAL], R6
		PUSH	1
		CALL	AtualizaPassaro
		RETN	2

Colisao:	MOV	M[CONT_CTRL_ADDR], R0
		RET

CriaObstaculo:	MOV	R1, OBSTACULOS
		ADD	R1, COLUNAS
		DEC	R1
		MOV	R7, PHYSICS_STEP
		MOV	M[CONT_STEP_ADDR], R7
		MOV	R7, 1
		MOV	M[CONT_CTRL_ADDR], R7
		PUSH	R1
		PUSH	R0
		CALL	Random
		POP	R2
		POP	R1
		MOV	M[R1], R2
		MOV	M[DEVE_CRIAR_OSBT], R0
		MOV	R1, INTERV_OBSTAC
		MOV	M[CONTADOR_OBST], R1
		PUSH	R0
		CALL	AtualizaObst
		PUSH	1
		CALL	AtualizaObst
		RET

MoveObstaculos:	PUSH	R0
		CALL	AtualizaObst
		MOV	R1, COLUNAS
		DEC	R1
		MOV	R2, OBSTACULOS
		ADD	R2, R1
		MOV	R3, R0
CicloMoveObst:	CMP	R1, R0
		BR.N	FimCicloMO
		MOV	R4, M[R2]
		MOV	M[R2], R3
		MOV	R3, R4
		DEC	R1
		DEC	R2
		BR	CicloMoveObst
FimCicloMO:	CMP	M[CONTADOR_OBST], R0
		BR.P	tempAt3
		MOV	R1, 1
		MOV	M[DEVE_CRIAR_OSBT], R1
tempAt3:	DEC	M[CONTADOR_OBST]
		MOV	M[DEVE_MOVER_OBST], R0
		MOV	R1, M[V_OBSTACULOS]
		MOV	M[CONT_MOV_OBST], R1
		PUSH	1
		CALL	AtualizaObst
FimMoveObst:	RET

SobePassaro:	MOV	R1, IMPULSO_SUBIDA
		MOV	M[VELOCIDADE], R1
		MOV	M[DEVE_SUBIR], R0
		RET

PhysicsStep:	CMP	M[NIVEL_MUDOU], R0
		BR.Z	ContPhysicsStep
		MOV	R1, M[NIVEL]
		ADD	R1, VELOCIDADES
		MOV	R2, M[R1]
		MOV	M[V_OBSTACULOS], R2
		CALL	AtualizaLeds
		MOV	M[NIVEL_MUDOU], R0
ContPhysicsStep:MOV	R1, M[VELOCIDADE]
		SUB	R1, GRAV_POR_STEP
		MOV	M[VELOCIDADE], R1
		MOV	R2, M[LINHA_ATUAL]
		SUB	R2, R1
		MOV	M[LINHA_ATUAL], R2
		SHR	R2, 8
		PUSH	R2
		PUSH	M[P_COLUNA_ATUAL]
		CALL	MudaPassaro
		MOV	M[DEVE_PYSC_STEP], R0
		RET

Inicio:		MOV	R7, SP_INICIAL
		MOV	SP, R7
		MOV	R7, INT_MASK
		MOV	M[INT_MASK_ADDR], R7
		ENI
		MOV	R7, FFFFh
		MOV	M[DISPLAY_C_ADDR], R7
		MOV	R1, M[VELOCIDADES]
		MOV	M[V_OBSTACULOS], R1
		PUSH	Vartext1
		PUSH	11
		PUSH	34
		CALL	EscreveStr
		PUSH	Vartext2
		PUSH	13
		PUSH	25
		CALL	EscreveStr
EsperaInicio:	CMP 	M[COMECAR_JOGO], R0
		BR.Z	ComecaJogo
		INC	M[N_SEQUENCIA]
		BR	EsperaInicio

ComecaJogo:	CALL	LimpaJanela
		CALL	DesenhaLimites
		PUSH	1
		CALL	AtualizaPassaro
		CALL	AtualizaLeds
		MOV	R7, PHYSICS_STEP
		MOV	M[CONT_STEP_ADDR], R7
		MOV	R7, 1
		MOV	M[CONT_CTRL_ADDR], R7
Ciclo1:		CMP	M[DEVE_PYSC_STEP], R0
		BR.Z	Ciclo2
		CALL	PhysicsStep
Ciclo2:		CMP	M[DEVE_SUBIR], R0
		BR.Z	Ciclo3
		CALL	SobePassaro
Ciclo3:		CMP	M[DEVE_CRIAR_OSBT], R0
		BR.Z	Ciclo4
		CALL	CriaObstaculo
Ciclo4:		CMP	M[DEVE_MOVER_OBST], R0
		BR.Z	Ciclo1
		CALL	MoveObstaculos
		BR	Ciclo1
; Passaro Bamboleante

; CONSTANTES
SP_INICIAL	EQU	FDFFh

DISPLAY_O_ADDR	EQU	FFFEh
DISPLAY_C_ADDR	EQU	FFFCh
INT_MASK_ADDR	EQU	FFFAh
CONT_STEP_ADDR	EQU	FFF6h
CONT_CTRL_ADDR	EQU	FFF7h

INT_MASK	EQU	1000000000000001b

COLUNAS		EQU	79
LINHAS		EQU	24

CHAR_TRACO	EQU	2Dh
CHAR_O		EQU	4Fh
CHAR_X		EQU	58h
CHAR_>		EQU	3Eh
PHYSICS_STEP	EQU	1
V_SUBIDA	EQU	1
LINHAS_SOBE	EQU	3

INTERV_OBSTAC	EQU	5

V_OBSTACULOS_1	EQU	10
V_OBSTACULOS_2	EQU	8
V_OBSTACULOS_3	EQU	6
V_OBSTACULOS_4	EQU	4
V_OBSTACULOS_5	EQU	2
V_OBSTACULOS_6	EQU	1

; VARIAVEIS
		ORIG	8000h
P_LINHA_ATUAL	STR	11
P_COLUNA_ATUAL	STR	19
FALTA_SUBIR	STR	0

V_OBSTACULOS	STR	0
CONT_MOV_OBST	STR	0
CONTADOR_OBST	STR	0

OBSTACULOS	TAB	COLUNAS

DEVE_SUBIR	STR	0
DEVE_CRIAR_OSBT	STR	0
DEVE_MOVER_OBST	STR	0

; Tabela de interrupcoes
		ORIG	FE00h
INT0		WORD	i0Premido
		ORIG	FE0Fh
INT15		WORD	tempAtivado

		ORIG	0000h
		JMP	Inicio

i0Premido:	MOV	R1, LINHAS_SOBE
		MOV	M[FALTA_SUBIR], R1
		RTI

tempAtivado:	CMP	M[FALTA_SUBIR], R0
		BR.Z	tempAt1
		MOV	R1, 1
		MOV	M[DEVE_SUBIR], R1
tempAt1:	CMP	M[CONT_MOV_OBST], R0
		BR.P	tempAt2
		MOV	R1, 1
		MOV	M[DEVE_MOVER_OBST], R1
tempAt2:	DEC	M[CONT_MOV_OBST]
		MOV	R7, PHYSICS_STEP
		MOV	M[CONT_STEP_ADDR], R7
		MOV	R7, 1
		MOV	M[CONT_CTRL_ADDR], R7
FimTempAtivado:	RTI

LimpaJanela:	MOV	R1, R0
		MOV	R2, R0
CicloLinhas:	CMP	R1, LINHAS
		BR.NN	FimAJ
CicloColunas:	CMP	R2, COLUNAS
		BR.NN	FimColunas
		MOV	R4, R1
		SHL	R4, 8
		ADD	R4, R2
		MOV	M[DISPLAY_C_ADDR], R4
		MOV	R5, 20h
		MOV	M[DISPLAY_O_ADDR], R5
		INC	R2
		BR	CicloColunas
FimColunas:	MOV	R2, R0
		INC	R1
		BR	CicloLinhas
FimAJ:		RET

AtualizaJanela:	CALL	LimpaJanela
		CALL	DesenhaLimites
		CALL	DesenhaPassaro
		RET

; DesenhaLimites: Esta funcao desenha os limites superior e infeior do ecra de jogo
; Variavies:	R1: linha do limite a ser desenhado
;		R2: coluna atual
;		R3: contem a linha atual, apos um shift left, 8 posicoes
;		R4
;		R5: Contem o CHAR_TRACO
DesenhaLimites:	MOV	R5, CHAR_TRACO
		MOV	R1, 0
		MOV	R2, 0
		MOV	R3, R1
		SHL	R3, 8
DL1:		CMP	R2, COLUNAS
		BR.P	FimDL1
		MOV	R4, R2
		ADD	R4, R3
		MOV	M[DISPLAY_C_ADDR], R4
		MOV	M[DISPLAY_O_ADDR], R5
		INC	R2
		BR	DL1
FimDL1:		NOP
		MOV	R1, 23
		MOV	R2, 0
		MOV	R3, R1
		SHL	R3, 8
DL2:		CMP	R2, COLUNAS
		BR.P	FimDL2
		MOV	R4, R2
		ADD	R4, R3
		MOV	M[DISPLAY_C_ADDR], R4
		MOV	M[DISPLAY_O_ADDR], R5
		INC	R2
		BR	DL2
FimDL2:		NOP
		RET

; DesenhaPassaro: 
DesenhaPassaro:	MOV	R1, M[P_LINHA_ATUAL]
		MOV	R2, M[P_COLUNA_ATUAL]
		MOV	R3, R1
		SHL	R3, 8
		ADD	R3, R2
		MOV	M[DISPLAY_C_ADDR], R3
		MOV	R4, CHAR_O
		MOV	M[DISPLAY_O_ADDR], R4
		INC	R2
		MOV	R3, R1
		SHL	R3, 8
		ADD	R3, R2
		MOV	M[DISPLAY_C_ADDR], R3
		MOV	R4, CHAR_>
		MOV	M[DISPLAY_O_ADDR], R4
		RET

; LimpaPassaro:
LimpaPassaro:	MOV	R1, M[P_LINHA_ATUAL]
		MOV	R2, M[P_COLUNA_ATUAL]
		MOV	R3, R1
		SHL	R3, 8
		ADD	R3, R2
		MOV	M[DISPLAY_C_ADDR], R3
		MOV	R4, 20h
		MOV	M[DISPLAY_O_ADDR], R4
		INC	R2
		MOV	R3, R1
		SHL	R3, 8
		ADD	R3, R2
		MOV	M[DISPLAY_C_ADDR], R3
		MOV	R4, 20h
		MOV	M[DISPLAY_O_ADDR], R4
		RET

DesenhaObst:	MOV	R1, R0
		MOV	R2, OBSTACULOS
		DEC	R2
CicloDeseObst:	CMP	R1, COLUNAS
		BR.P	FimDesenhaOb
		CMP	M[R2], R0
		BR.Z	FimCicloDO
		MOV	R3, LINHAS
		SUB	R3, 2
		MOV	R4, R1
		MOV	R5, R3
		SHL	R5, 8
		ADD	R5, R4
		MOV	M[DISPLAY_C_ADDR], R5
		MOV	R6, CHAR_X
		MOV	M[DISPLAY_O_ADDR], R6
FimCicloDO:	INC	R1
		INC	R2
		BR	CicloDeseObst
FimDesenhaOb:	RET

; MudaPassaro:
;		R1 = novaLinha
;		R2 = novaColuna

MudaPassaro:	CALL	LimpaPassaro
		MOV	R1, M[SP+3]
		MOV	R2, M[SP+2]
		MOV	M[P_LINHA_ATUAL], R1
		MOV	M[P_COLUNA_ATUAL], R2
		CALL	DesenhaPassaro
		RETN	2

SobePassaro:	MOV	R1, M[P_LINHA_ATUAL]
		SUB	R1, V_SUBIDA
		PUSH	R1
		PUSH	M[P_COLUNA_ATUAL]
		CALL	MudaPassaro
		DEC	M[FALTA_SUBIR]
		MOV	M[DEVE_SUBIR], R0
		RET

CriaObstaculo:	MOV	R1, OBSTACULOS
		ADD	R1, COLUNAS
		DEC	R1
		MOV	R7, PHYSICS_STEP
		MOV	M[CONT_STEP_ADDR], R7
		MOV	R7, 1
		MOV	M[CONT_CTRL_ADDR], R7
		MOV	R2, 1
		MOV	M[R1], R2
		MOV	M[DEVE_CRIAR_OSBT], R0
		MOV	R1, INTERV_OBSTAC
		MOV	M[CONTADOR_OBST], R1
		CALL	DesenhaObst
		RET

MoveObstaculos:	MOV	R1, COLUNAS
		DEC	R1
		MOV	R2, OBSTACULOS
		ADD	R2, R1
		MOV	R3, R0
CicloMoveObst:	CMP	R1, R0
		BR.N	FimCicloMO
		MOV	R4, M[R2]
		MOV	M[R2], R3
		MOV	R3, R4
		DEC	R1
		DEC	R2
		BR	CicloMoveObst
FimCicloMO:	CMP	M[CONTADOR_OBST], R0
		BR.P	tempAt3
		MOV	R1, 1
		MOV	M[DEVE_CRIAR_OSBT], R1
tempAt3:	DEC	M[CONTADOR_OBST]
		MOV	M[DEVE_MOVER_OBST], R0
		MOV	R1, M[V_OBSTACULOS]
		MOV	M[CONT_MOV_OBST], R1
		CALL	AtualizaJanela
		CALL	DesenhaObst
FimMoveObst:	RET

Inicio:		MOV	R7, SP_INICIAL
		MOV	SP, R7
		MOV	R7, INT_MASK
		MOV	M[INT_MASK_ADDR], R7
		ENI
		MOV	R7, FFFFh
		MOV	M[DISPLAY_C_ADDR], R7

		MOV	R1, V_OBSTACULOS_1
		MOV	M[V_OBSTACULOS], R1
		CALL	AtualizaJanela

		MOV	R7, PHYSICS_STEP
		MOV	M[CONT_STEP_ADDR], R7
		MOV	R7, 1
		MOV	M[CONT_CTRL_ADDR], R7
Ciclo1:		CMP	M[DEVE_SUBIR], R0
		BR.Z	Ciclo2
		DSI
		CALL	SobePassaro
		ENI
Ciclo2:		CMP	M[DEVE_CRIAR_OSBT], R0
		BR.Z	Ciclo3
		DSI
		CALL	CriaObstaculo
		ENI
Ciclo3:		CMP	M[DEVE_MOVER_OBST], R0
		BR.Z	Ciclo1
		DSI
		CALL	MoveObstaculos
		ENI
		BR	Ciclo1
